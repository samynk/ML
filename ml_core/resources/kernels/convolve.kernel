__kernel void convolution(
    const __global float* input,
    __constant float* filter,
    __global float* output,
    const int2 iDim,
    const int2 fDim,
    const int2 oDim
)
{
    int gCol = get_global_id(0);
    int gRow = get_global_id(1);

    float sum = 0;
    // column major
    // frow is row coordinate of filter 

    for(int fRow = 0;  fRow < fDim.y; ++fRow){
        int inputRow = gRow + fRow;   
        for(int fCol = 0;fCol< fDim.x;++fCol){
            int inputCol = gCol + fCol;
            int inputColOffset = inputCol * iDim.y;
            int inputI = inputRow  + inputColOffset;
            
            int filterColOffset = fCol * fDim.y;
            int filterI = fRow + filterColOffset;
            sum += input[inputI] * filter[filterI];
        }
    }
    output[ gRow + gCol * oDim.y ] = sum;
}

__kernel void batchConvolution(
    const __global float* input,
    const __global float* filter,
    __global float* output,
    const int2 iDim,
    const int2 fDim,
    const int filtersPerSlice,
    const int2 oDim,
    const int stride
)
{
    int gCol = get_global_id(0)* stride;
    int gRow = get_global_id(1)* stride;
    int filterNr = get_global_id(2);

    int inputSliceOffset = iDim.x * iDim.y * (filterNr/filtersPerSlice);
    int filterSliceOffset = fDim.x * fDim.y * filterNr;
    int outputSliceOffset = oDim.x * oDim.y * filterNr;

    float sum = 0;
    for(int fRow = 0;  fRow < fDim.y; ++fRow){
        int inputRow = gRow + fRow;   
        for(int fCol = 0;fCol< fDim.x;++fCol){
            int inputCol = gCol + fCol;
            int inputColOffset = inputCol * iDim.y;
            int inputI = inputRow  + inputColOffset+inputSliceOffset;
            
            int filterColOffset = fCol * fDim.y;
            int filterI = fRow + filterColOffset + filterSliceOffset ;
            sum += input[inputI] * filter[filterI];
        }
    }
    int oIndex = gRow + gCol * oDim.y + outputSliceOffset;
    output[ oIndex ] = sum;
}

__kernel void batchCorrelate(
    const __global float* input,
    const __global float* filter,
    __global float* output,
    const int2 iDim,
    const int2 fDim,
    const int filtersPerSlice,
    const int2 oDim,
    const int stride
)
{
    int gCol = get_global_id(0)* stride;
    int gRow = get_global_id(1)* stride;
    int filterNr = get_global_id(2);

    // int filterSliceSize = fDim.x*fDim.y;
    int inputSliceOffset = iDim.x * iDim.y * (filterNr/filtersPerSlice);
    int filterSliceOffset = fDim.x * fDim.y * filterNr;
    int outputSliceOffset = oDim.x * oDim.y * filterNr;

    float sum = 0;
    for(int fRow = 0;  fRow < fDim.y; ++fRow){
        int inputRow = gRow + fRow;   
        for(int fCol = 0;fCol< fDim.x;++fCol){
            int inputCol = gCol + fCol;
            int inputColOffset = inputCol * iDim.y;
            int inputI = inputRow  + inputColOffset + inputSliceOffset;
            
            int fColOffset = (fDim.x - fCol - 1) * fDim.y;
            int fRowOffset = (fDim.y - fRow - 1);
            int filterI = fRowOffset + fColOffset + filterSliceOffset ;
            sum += input[inputI] * filter[filterI];
        }
    }
    output[ gRow + gCol * oDim.y + outputSliceOffset ] = sum;
}

__kernel void batchBackpropCorrelate(
    const __global float* input,
    const __global float* filter,
    __global float* output,
    const int2 iDim,
    const int2 fDim,
    const int filtersPerSlice,
    const int2 oDim,
    const int stride
)
{
    int gCol = get_global_id(0)* stride;
    int gRow = get_global_id(1)* stride;
    int filterNr = get_global_id(2);

    int filterSliceSize = fDim.x*fDim.y;
    int inputSliceSize = iDim.x*iDim.y;
    int filterBlockOffset = filterSliceSize * filtersPerSlice * filterNr;
    int inputBlockOffset = inputSliceSize * filtersPerSlice * filterNr;

    int outputSliceOffset = oDim.x * oDim.y * filterNr;

    float sum = 0;
    for(int iSlice = 0; iSlice < filtersPerSlice; ++iSlice){
        int filterOffset = filterSliceSize * iSlice;
        int inputOffset = inputSliceSize * iSlice;
        
        for(int fRow = 0;  fRow < fDim.y; ++fRow){
            int inputRow = gRow + fRow;   
            for(int fCol = 0;fCol< fDim.x;++fCol){
                int inputCol = gCol + fCol;
                int inputColOffset = inputCol * iDim.y;
                int inputI = inputRow  + inputColOffset + inputOffset + inputBlockOffset;
            
                int fColOffset = (fDim.x - fCol - 1) * fDim.y;
                int fRowOffset = (fDim.y - fRow - 1);
                int filterI = fRowOffset + fColOffset + filterOffset + filterBlockOffset;
                sum += input[inputI] * filter[filterI];
            }
        }
    }
    output[ gRow + gCol * oDim.y + outputSliceOffset ] = sum;
}